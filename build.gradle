plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    group = 'dev.wony'
    version = '0.0.1-SNAPSHOT'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        jvmArgs '-Dfile.encoding=UTF-8'
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:4.0.0'
        }
        dependencies {
            dependency 'com.squareup.okhttp3:okhttp:4.12.0'
            dependency 'com.squareup.okhttp3:mockwebserver:4.12.0'
            dependency 'org.apache.httpcomponents.client5:httpclient5:5.3'
        }
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // Commons
        implementation 'org.apache.commons:commons-lang3:3.14.0'
        implementation 'com.google.guava:guava:33.0.0-jre'

        // Logging
        implementation 'ch.qos.logback:logback-classic'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.mockito:mockito-core'
        testImplementation 'com.fasterxml.jackson.core:jackson-databind'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}
